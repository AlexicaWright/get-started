= Northwind Graph
:neo4j-version: 4.4.0
:imagesdir: https://guides.neo4j.com/auradb/get-started/img/

== Get Started

NOTE: If you are new to graphs and opened this guide in Neo4j AuraDB to learn, please switch to "Neo4j Workspace" in your user-account.

Then you get a fully integrated guide for import, exploration, query.

.AuraDB Setting to switch to Workspace
image::auradb-workspace-setting.png[]

.AuraDB Button to Open Workspace
image::auradb-workspace-button.png[]

== From RDBMS to Graph, using a classic dataset

The__Northwind Graph__ demonstrates how to migrate from a relational database to Neo4j.

The transformation is iterative and deliberate, emphasizing the conceptual shift from relational tables to the nodes and relationships of a graph.

This guide will show you how to:

1.  Load: create data from external CSV files
2.  Index: index nodes based on label
3.  Relate: transform foreign key references into data relationships
4.  Promote: transform join records into relationships


== Product Catalog

Northwind sells food products in a few categories, provided by suppliers. Let's start by loading the product catalog tables.

The load statements to the right require public internet access.

`LOAD CSV` will retrieve a CSV file from a valid URL, applying a Cypher statement to each row using a named map (here we're using the
name `row`).

image:https://dev.assets.neo4j.com/wp-content/uploads/20160211151109/product-category-supplier.png[image]

== Import Products, Categories, Suppliers

.Create constraints
[source,cypher]
----
CREATE CONSTRAINT product_productid IF NOT EXISTS FOR (p:Product) REQUIRE (p.productID) IS UNIQUE;
CREATE CONSTRAINT category_categoryid IF NOT EXISTS FOR (c:Category) REQUIRE (c.categoryID) IS UNIQUE;
CREATE CONSTRAINT supplier_supplierid IF NOT EXISTS FOR (s:Supplier) REQUIRE (s.supplierID) IS UNIQUE;
----

.Load products, categories and suppliers
[source,cypher]
----
// load products
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/products.csv" AS row
MERGE (n:Product {productID : row.productID})
ON CREATE SET n += row,
  n.unitPrice = toFloat(row.unitPrice),
  n.unitsInStock = toInteger(row.unitsInStock), 
  n.unitsOnOrder = toInteger(row.unitsOnOrder),
  n.reorderLevel = toInteger(row.reorderLevel), 
  n.discontinued = (row.discontinued <> "0");

// load categories
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/categories.csv" AS row
MERGE (n:Category {categoryID : row.categoryID})
ON CREATE SET n += row;

// load suppliers
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/suppliers.csv" AS row
MERGE (n:Supplier {supplierID : row.supplierID})
ON CREATE SET n += row;
----

== Product Catalog Graph

The products, categories and suppliers are related through foreign key references. 
Let's promote those to data relationships to realize the graph.

image:https://dev.assets.neo4j.com/wp-content/uploads/20160211151108/product-graph.png[image]

=== Create data relationships

.Connect products to categories
[source,cypher]
----
MATCH (p:Product),(c:Category)
WHERE p.categoryID = c.categoryID
MERGE (p)-[:PART_OF]->(c);
----

NOTE: you only need to compare property values like this when first creating relationships.
In principle you can delete the `categoryID` from product afterwards.

Calculate join, materialize relationship. (See https://neo4j.com/developer/guide-importing-data-and-etl[importing guide^] for more details)

.Connect products to suppliers
[source,cypher]
----
MATCH (p:Product),(s:Supplier)
WHERE p.supplierID = s.supplierID
MERGE (s)-[:SUPPLIES]->(p);
----

.Show some part of the product catalog graph
[source,cypher]
----
MATCH path=(s:Supplier)-[:SUPPLIES]->(:Product)-[:PART_OF]->(c:Category)
RETURN path LIMIT 50;
----

== Querying Product Catalog Graph

Lets try some queries using patterns.

image:https://dev.assets.neo4j.com/wp-content/uploads/20160211151108/product-graph.png[image]

=== Query using patterns

.List the product categories provided by each supplier
[source,cypher]
----
MATCH (s:Supplier)-[:SUPPLIES]->(:Product)-[:PART_OF]->(c:Category)
RETURN s.companyName as Supplier, collect(distinct c.categoryName) as Categories
----

.List suppliers for the "Produce" category
[source,cypher]
----
MATCH (c:Category {categoryName:'Produce'})<--(:Product)<--(s:Supplier)
RETURN DISTINCT s.companyName as ProduceSuppliers
----

== Customer Orders

Northwind customers place orders which may detail multiple products. 

image::https://dev.assets.neo4j.com/wp-content/uploads/20160211151108/customer-orders.png[]

=== Create Constraints

[source,cypher]
----
CREATE CONSTRAINT customer_customerid IF NOT EXISTS FOR (c:Customer) REQUIRE (c.customerID) IS UNIQUE;
CREATE CONSTRAINT order_orderid IF NOT EXISTS FOR (o:Order) REQUIRE (o.orderID) IS UNIQUE;
----

=== Load and index records

.Load customers and orders
[source,cypher]
----
// load customers
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/customers.csv" AS row
MERGE (n:Customer {customerID: row.customerID})
ON CREATE SET n += row;

// load orders
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/orders.csv" AS row
MERGE (n:Order {orderID: row.orderID})
ON CREATE SET n += row;

// Connect customers to their orders
MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
MERGE (c)-[:PURCHASED]->(o);
----

== Customer Order Graph

Notice that Order Details are always part of an Order and that they__relate__ the Order to a Product â€” they're a join table. 
Join tables are always a sign of a data relationship, indicating shared information between two other records.

Here, we'll directly promote each OrderDetail record into a relationship
in the graph. 

image::https://dev.assets.neo4j.com/wp-content/uploads/20160211151107/order-graph.png[]


=== Load and Connect Order Details

.Load order details and use them to connect orders to products
[source,cypher]
----
LOAD CSV WITH HEADERS FROM "https://data.neo4j.com/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
MERGE (o)-[details:ORDERS]->(p)
ON CREATE SET 
  details.unitPrice = toFloat(row.unitPrice),
  details.discount = toFloat(row.discount),  
  details.quantity = toInteger(row.quantity)
----

== Query using patterns

.Show some part of the customer order graph
[source,cypher]
----
MATCH path = (cust:Customer {contactName: "Blauer See Delikatessen"})-[:PURCHASED]->(:Order)
      -[o:ORDERS]->(p:Product)-[:PART_OF]->(c:Category)
RETURN path;
----


.Find total quantity per customer in the "Produce" category
[source,cypher]
----
MATCH (cust:Customer)-[:PURCHASED]->(:Order)-[o:ORDERS]->(p:Product),
      (p)-[:PART_OF]->(c:Category {categoryName:"Produce"})
RETURN DISTINCT cust.contactName as CustomerName, SUM(o.quantity) AS TotalProductsPurchased
----

== More Resources

* https://neo4j.com/developer/guide-importing-data-and-etl/[Full Northwind import example]
* https://neo4j.com/developer[Developer resources^]

